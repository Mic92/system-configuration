pkgname='todolist'
pkgver=0.1
pkgrel=2
pkgdesc='Minimalistic web-based todolist app'
arch=('i686' 'x86_64')
url='https://github.com/majewsky/todolist'
license=('AGPL3')
depends=()
makedepends=('go')
source=(
    "https://github.com/majewsky/${pkgname}/archive/v${pkgver}.tar.gz"
    'todolist.service'
)
md5sums=(
    'dece15c64895cbde02aeebdaf92f89e0'
    'SKIP'
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p gopath/src/github.com/majewsky
    ln -s $PWD gopath/src/github.com/majewsky/todolist
    env GOPATH=$PWD/gopath make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m 0755 todolist "${pkgdir}/usr/bin/todolist"
    install -D -m 0644 README.md "${pkgdir}/usr/share/doc/todolist/README.md"
    install -D -m 0644 "${srcdir}/todolist.service" "${pkgdir}/usr/lib/systemd/system/todolist.service"
}
