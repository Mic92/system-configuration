pkgname='holodeck-damogran'
pkgver=20150805
pkgrel=5
pkgdesc='holodeck: services on Damogran'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
source=(
    # TODO: smartd to monitor RAID
    # TODO: setup for Minecraft server
    '.env'
    '90-firewall.conf.in'
    'dnsmasq.conf.in'
    'gummiboot-entry.conf.in'
    'gummiboot-loader.conf'
    'hostapd.conf.in'
    'iptables.rules.in'
    'mkinitcpio.conf.holoscript'
    'nginx.conf.in'
    'nginx_50x.html'
    'lan.network.in'
    'wan.network.in'
    'wlan.network.in'
)
install='holodeck-damogran.install'

build() {
    cd "${srcdir}"
    echo damogran > hostname

    # check that all configuration is present
    source "${srcdir}/.env"
    if [ -z "$DAMOGRAN_IFACE_LAN" -o -z "$DAMOGRAN_IFACE_WLAN" -o -z "$DAMOGRAN_IFACE_WAN" \
      -o -z "$DAMOGRAN_NET_LAN"   -o -z "$DAMOGRAN_NET_WLAN" \
      -o -z "$DAMOGRAN_WLAN_SSID" -o -z "$DAMOGRAN_WLAN_PSK" \
      -o -z "$DAMOGRAN_HTTP_STATIC_DOMAIN" -o -z "$DAMOGRAN_HTTP_STATIC_PATH" \
      -o -z "$DAMOGRAN_ROOT_PARTUUID" ]; then
        echo "Some variables are not defined by .env; stopping." >&2
        return 1
    fi

    # replace configuration variables in source files
    for file in 90-firewall.conf dnsmasq.conf gummiboot-entry.conf hostapd.conf iptables.rules nginx.conf lan.network wan.network wlan.network; do
        perl -pe 's{\$\{(\w+?)\}}{$ENV{$1}//$&}eg' < "${file}.in" > $file
    done
}

package() {
    # runtime-only dependencies
    depends+=(
        'holo'
        'hologram-base'
        'hologram-multimedia-base'
        'hologram-damogran-dyndns'
        'hologram-user-dbus'
        'hologram-damogran-audio'
        # boot loader (NOTE: no intel-ucode since Damogran has an AMD CPU and chipset)
        'gummiboot'
        # network setup (local DHCP, local DNS, wireless AP, NAT from internal networks to external internet)
        'dnsmasq'
        'hostapd'
        'iptables'
        # public services
        'nginx'
        # intranet services
        'jre7-openjdk-headless' # for Minecraft
    )

    # TODO: Should sshd.service be enabled in the base holodeck? I'd rather put
    # it in a server holodeck, if such a thing ever comes to exist.
    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants/"
    ln -sf /usr/lib/systemd/system/dnsmasq.service           "${pkgdir}/etc/systemd/system/multi-user.target.wants/dnsmasq.service"
    ln -sf /usr/lib/systemd/system/hostapd.service           "${pkgdir}/etc/systemd/system/multi-user.target.wants/hostapd.service"
    ln -sf /usr/lib/systemd/system/iptables.service          "${pkgdir}/etc/systemd/system/multi-user.target.wants/iptables.service"
    ln -sf /usr/lib/systemd/system/nginx.service             "${pkgdir}/etc/systemd/system/multi-user.target.wants/nginx.service"
    ln -sf /usr/lib/systemd/system/sshd.service              "${pkgdir}/etc/systemd/system/multi-user.target.wants/sshd.service"
    ln -sf /usr/lib/systemd/system/systemd-resolved.service  "${pkgdir}/etc/systemd/system/multi-user.target.wants/systemd-resolved.service"

    install -D -m 0644 dnsmasq.conf                "${pkgdir}/holo/repo/etc/dnsmasq.conf"
    install -D -m 0644 gummiboot-entry.conf        "${pkgdir}/boot/loader/entries/arch.conf"
    install -D -m 0644 gummiboot-loader.conf       "${pkgdir}/boot/loader/loader.conf"
    install -D -m 0644 hostapd.conf                "${pkgdir}/etc/hostapd/hostapd.conf"
    install -D -m 0644 hostname                    "${pkgdir}/etc/hostname"
    install -D -m 0644 iptables.rules              "${pkgdir}/etc/iptables/iptables.rules"
    install -D -m 0755 mkinitcpio.conf.holoscript  "${pkgdir}/holo/repo/etc/mkinitcpio.conf.holoscript"
    install -D -m 0644 nginx.conf                  "${pkgdir}/holo/repo/etc/nginx/nginx.conf"
    install -D -m 0644 nginx_50x.html              "${pkgdir}/etc/nginx/pages/nginx_50x.html"
    install -D -m 0755 90-firewall.conf            "${pkgdir}/etc/sysctl.d/90-firewall.conf"
    install -D -m 0644 lan.network                 "${pkgdir}/etc/systemd/network/lan.network"
    install -D -m 0644 wan.network                 "${pkgdir}/etc/systemd/network/wan.network"
    install -D -m 0644 wlan.network                "${pkgdir}/etc/systemd/network/wlan.network"
}
