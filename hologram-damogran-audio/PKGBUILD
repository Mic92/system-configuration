pkgname='hologram-damogran-audio'
pkgver=1.2.0
pkgrel=1
pkgdesc='hologram: PulseAudio and MPD service for Damogran'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
source=(
    'entities.toml'
    'mpd.conf'
    'pulseaudio-default.pa.holoscript'
    'pulseaudio-restart.conf'
)

install='hologram-damogran-audio.install'

package() {
    depends=(
        'holo>=0.4' # obviously :)
        'mpd'
        'pulseaudio'
        'pulseaudio-zeroconf'
    )

    # audio setup involves a systemd user session for the "pulse" user where
    # PulseAudio and MPD are managed
    install -D -m 0755 pulseaudio-default.pa.holoscript "${pkgdir}/usr/share/holo/repo/50-damogran/etc/pulse/default.pa.holoscript"
    install -D -m 0444 entities.toml                    "${pkgdir}/usr/share/holo/50-damogran-audio.toml"
    mkdir -p "${pkgdir}/home/pulse/.config/systemd/user/sockets.target.wants/"
    ln -sf /usr/lib/systemd/user/pulseaudio.socket      "${pkgdir}/home/pulse/.config/systemd/user/sockets.target.wants/pulseaudio.socket"
    mkdir -p "${pkgdir}/home/pulse/.config/systemd/user/default.target.wants/"
    ln -sf /usr/lib/systemd/user/pulseaudio.service     "${pkgdir}/home/pulse/.config/systemd/user/default.target.wants/pulseaudio.service"

    # enable lingering (i.e. persistent auto-spawned) user session for audio user
    mkdir -p "${pkgdir}/var/lib/systemd/linger/"
    touch "${pkgdir}/var/lib/systemd/linger/pulse"

    # auto-restart PulseAudio (sometimes it decides to shutdown spontaneously
    # when the last client disconnects)
    install -D -m 0755 pulseaudio-restart.conf "${pkgdir}/home/pulse/.config/systemd/user/pulseaudio.service.d/restart.conf"

    # MPD is started as a user service
    mkdir -p "${pkgdir}/home/pulse/.mpd/"

    install -D -m 0644 mpd.conf              "${pkgdir}/home/pulse/.mpdconf"
    ln -sf /usr/lib/systemd/user/mpd.service "${pkgdir}/home/pulse/.config/systemd/user/default.target.wants/mpd.service"
}
