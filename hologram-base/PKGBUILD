pkgname='hologram-base'
pkgver=20151103
pkgrel=1
pkgdesc='hologram: base installation'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
source=(
    '.env'
    'devenv-bootstrap.sh'
    'entities.toml'
    'journald.conf'
    'locale.conf'
    'locale.gen'
    'mirrorlist.holoscript'
    'pacman.conf.holoscript.in'
    'sudoers'
    'timesyncd.conf'
    'vconsole.conf'
)
install='hologram-base.install'

build() {
    # check that all configuration is present
    source "${srcdir}/.env"
    if [ -z "$AUTHORIZED_KEYS" -o -z "$DAMOGRAN_HTTP_STATIC_DOMAIN" ]; then
        echo "Some variables are not defined by .env; stopping." >&2
        return 1
    fi

    echo "$AUTHORIZED_KEYS" > "${srcdir}/authorized_keys"

    # replace configuration variables in source files
    for file in pacman.conf.holoscript; do
        perl -pe 's{\$\{(\w+?)\}}{$ENV{$1}//$&}eg' < "${file}.in" > $file
    done
}

package() {
    depends=(
        'holo>=0.4' # obviously :)
        # base system
        $(pacman -Sqg base | grep -v netctl)
        $(pacman -Sqg base-devel)
        # system components
        'haveged'
        'hologram-openssh'
        # assorted CLI tools
        'ack'
        'acpi'
        'atop'
        'dnsutils'
        'dosfstools'     # mkfs.vfat
        'git'
        'gptfdisk'
        'hdparm'
        'htop'
        'inotify-tools'
        'iperf'
        'irssi'
        'iw'
        'lsof'
        'net-tools'      # netstat
        'nmap'           # ncat
        'optipng'
        'pacgraph'
        'pwgen'
        'smartmontools'
        'rsync'
        'screen'
        'strace'
        'sshfs'
        'sudo'
        'tcpdump'
        'traceroute'
        'tree'
        'units'
        'unrar'
        'unzip'
        'vim'
        'wget'
        'whois'
        'yaourt'
        'zip'
        'zsh'
    )

    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants/"
    ln -sf /usr/lib/systemd/system/systemd-networkd.service  "${pkgdir}/etc/systemd/system/multi-user.target.wants/systemd-networkd.service"
    ln -sf /usr/lib/systemd/system/haveged.service           "${pkgdir}/etc/systemd/system/multi-user.target.wants/haveged.service"
    mkdir -p "${pkgdir}/etc/systemd/system/sysinit.target.wants/"
    ln -sf /usr/lib/systemd/system/systemd-timesyncd.service "${pkgdir}/etc/systemd/system/sysinit.target.wants/systemd-timesyncd.service"
    ln -sf /usr/share/zoneinfo/Europe/Berlin                 "${pkgdir}/etc/localtime"

    # pre-create some directories with special permission levels
    install -d -m 0700 "${pkgdir}/home/stefan/"
    install -d -m 0700 "${pkgdir}/home/stefan/.ssh/"
    install -d -m 0700 "${pkgdir}/home/stefan.devenv/"

    install -D -m 0600 authorized_keys        "${pkgdir}/home/stefan/.ssh/authorized_keys"
    install -D -m 0644 journald.conf          "${pkgdir}/etc/systemd/journald.conf.d/hologram-base.conf"
    install -D -m 0644 timesyncd.conf         "${pkgdir}/etc/systemd/timesyncd.conf.d/hologram-base.conf"
    install -D -m 0644 locale.conf            "${pkgdir}/etc/locale.conf"
    install -D -m 0644 locale.gen             "${pkgdir}/usr/share/holo/repo/00-base/etc/locale.gen"
    install -D -m 0755 mirrorlist.holoscript  "${pkgdir}/usr/share/holo/repo/00-base/etc/pacman.d/mirrorlist.holoscript"
    install -D -m 0755 pacman.conf.holoscript "${pkgdir}/usr/share/holo/repo/00-base/etc/pacman.conf.holoscript"
    install -D -m 0440 sudoers                "${pkgdir}/usr/share/holo/repo/00-base/etc/sudoers"
    install -D -m 0644 vconsole.conf          "${pkgdir}/etc/vconsole.conf"
    install -D -m 0444 entities.toml          "${pkgdir}/usr/share/holo/00-base.toml"

    # This is some extra setup for quick bootstrapping of my development
    # environment, which resides in a separate repo.
    mkdir -p "${pkgdir}/home/stefan.devenv/build"
    install -D -m 0755 devenv-bootstrap.sh    "${pkgdir}/home/stefan.devenv/bootstrap.sh"

    # TODO: krikkit has a modified yaourt.conf with SUDONOVERIF=1
    # (which "avoids multiple sudo checks when timestamp_timeout=0"); what is
    # all this?
}
