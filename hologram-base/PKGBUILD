pkgname='hologram-base'
pkgver=20150805
pkgrel=4
pkgdesc='hologram: base installation'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
replaces=('holodeck-base') # this used to be called "holodeck" incorrectly
source=(
    '.env'
    'devenv-bootstrap.sh'
    'journald.conf'
    'locale.conf'
    'locale.gen'
    'mirrorlist.holoscript'
    'pacman.conf.holoscript'
    'sudoers'
    'timesyncd.conf'
    'vconsole.conf'
)
install='hologram-base.install'

build() {
    # check that all configuration is present
    source "${srcdir}/.env"
    if [ -z "$AUTHORIZED_KEYS" ]; then
        echo "Some variables are not defined by .env; stopping." >&2
        return 1
    fi

    echo "$AUTHORIZED_KEYS" > "${srcdir}/authorized_keys"
}

package() {
    depends=(
        'holo' # obviously :)
        # base system
        $(pacman -Sqg base | grep -v netctl)
        $(pacman -Sqg base-devel)
        # system components
        'haveged'
        'hologram-openssh'
        # assorted CLI tools
        'ack'
        'acpi'
        'atop'
        'dnsutils'
        'dosfstools'     # mkfs.vfat
        'git'
        'gptfdisk'
        'htop'
        'inotify-tools'
        'lsof'
        'net-tools'      # netstat
        'nmap'           # ncat
        'pwgen'
        'smartmontools'
        'rsync'
        'screen'
        'strace'
        'sudo'
        'tcpdump'
        'traceroute'
        'tree'
        'units'
        'unrar'
        'unzip'
        'vim'
        'wget'
        'whois'
        'zip'
        'zsh'
    )

    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants/"
    ln -sf /usr/lib/systemd/system/systemd-networkd.service  "${pkgdir}/etc/systemd/system/multi-user.target.wants/systemd-networkd.service"
    ln -sf /usr/lib/systemd/system/haveged.service           "${pkgdir}/etc/systemd/system/multi-user.target.wants/haveged.service"
    mkdir -p "${pkgdir}/etc/systemd/system/sysinit.target.wants/"
    ln -sf /usr/lib/systemd/system/systemd-timesyncd.service "${pkgdir}/etc/systemd/system/sysinit.target.wants/systemd-timesyncd.service"
    ln -sf /usr/share/zoneinfo/Europe/Berlin                 "${pkgdir}/etc/localtime"

    install -D -m 0600 authorized_keys        "${pkgdir}/home/stefan/.ssh/authorized_keys"
    install -D -m 0644 journald.conf          "${pkgdir}/etc/systemd/journald.conf.d/hologram-base.conf"
    install -D -m 0644 timesyncd.conf         "${pkgdir}/etc/systemd/timesyncd.conf.d/hologram-base.conf"
    install -D -m 0644 locale.conf            "${pkgdir}/etc/locale.conf"
    install -D -m 0644 locale.gen             "${pkgdir}/holo/repo/etc/locale.gen"
    install -D -m 0755 mirrorlist.holoscript  "${pkgdir}/holo/repo/etc/pacman.d/mirrorlist.holoscript"
    install -D -m 0755 pacman.conf.holoscript "${pkgdir}/holo/repo/etc/pacman.conf.holoscript"
    install -D -m 0440 sudoers                "${pkgdir}/holo/repo/etc/sudoers"
    install -D -m 0644 vconsole.conf          "${pkgdir}/etc/vconsole.conf"

    # This is some extra setup for quick bootstrapping of my development
    # environment, which resides in a separate repo.
    mkdir -p "${pkgdir}/home/stefan.devenv/build"
    install -D -m 0755 devenv-bootstrap.sh    "${pkgdir}/home/stefan.devenv/bootstrap.sh"
}
