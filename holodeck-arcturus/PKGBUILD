pkgname='holodeck-arcturus'
pkgver=20150812
pkgrel=2
pkgdesc='holodeck: services on Arcturus'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
source=(
    '.env'
    'mkinitcpio.conf.holoscript'
    'prettyprompt.sh'
    'systemd-boot-entry.conf.in'
    'systemd-boot-loader.conf'
    'lan.network.in'
    'wlan.network.in'
    'wpa_supplicant.conf.in'
)
install='holodeck-arcturus.install'

build() {
    cd "${srcdir}"
    echo arcturus > hostname

    # check that all configuration is present
    source "${srcdir}/.env"
    if [ -z "$DAMOGRAN_NET_LAN"   -o -z "$DAMOGRAN_NET_WLAN" \
      -o -z "$DAMOGRAN_WLAN_SSID" -o -z "$DAMOGRAN_WLAN_PSK" \
      -o -z "$DAMOGRAN_HTTP_STATIC_DOMAIN" -o -z "$DAMOGRAN_HTTP_STATIC_PATH" \
      -o -z "$ARCTURUS_ROOT_PARTUUID"      -o -z "$ARCTURUS_IFACE_WLAN" ]; then
        echo "Some variables are not defined by .env; stopping." >&2
        return 1
    fi

    # replace configuration variables in source files
    for file in systemd-boot-entry.conf lan.network wlan.network wpa_supplicant.conf; do
        perl -pe 's{\$\{(\w+?)\}}{$ENV{$1}//$&}eg' < "${file}.in" > $file
    done
}

package() {
    depends=(
        'hologram-kde-desktop'
        'hologram-dev-tools'
        'hologram-games'
        # support for Intel CPU/GPU
        'intel-ucode'
        'xf86-video-intel'
        'libva-intel-driver'
        # network setup
        'wpa_supplicant'
    )

    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants/"
    ln -sf /usr/lib/systemd/system/systemd-resolved.service "${pkgdir}/etc/systemd/system/multi-user.target.wants/systemd-resolved.service"
    ln -sf /usr/lib/systemd/system/wpa_supplicant@.service  "${pkgdir}/etc/systemd/system/multi-user.target.wants/wpa_supplicant@${ARCTURUS_IFACE_WLAN}.service"
    mkdir -p "${pkgdir}/holo/repo/etc/"
    ln -sf /run/systemd/resolve/resolv.conf                 "${pkgdir}/holo/repo/etc/resolv.conf"

    install -D -m 0644 hostname                    "${pkgdir}/etc/hostname"
    install -D -m 0755 mkinitcpio.conf.holoscript  "${pkgdir}/holo/repo/etc/mkinitcpio.conf.holoscript"
    install -D -m 0755 prettyprompt.sh             "${pkgdir}/etc/profile.d/prettyprompt.sh"
    install -D -m 0644 systemd-boot-entry.conf     "${pkgdir}/boot/loader/entries/arch.conf"
    install -D -m 0644 systemd-boot-loader.conf    "${pkgdir}/boot/loader/loader.conf"
    install -D -m 0644 lan.network                 "${pkgdir}/etc/systemd/network/lan.network"
    install -D -m 0644 wlan.network                "${pkgdir}/etc/systemd/network/wlan.network"
    install -D -m 0644 wpa_supplicant.conf         "${pkgdir}/etc/wpa_supplicant/wpa_supplicant-${ARCTURUS_IFACE_WLAN}.conf"
}
