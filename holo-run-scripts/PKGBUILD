pkgname='holo-run-scripts'
_actual_pkgver=1.0-beta.1
pkgver=${_actual_pkgver//-/}
pkgrel=1
pkgdesc='Holo plugin for running custom provisioning scripts'
arch=('i686' 'x86_64')
url='http://holocm.org'
license=('GPL3')
depends=(
    'HOLO_API_VERSION=1'
)
makedepends=('perl')
source=("https://github.com/holocm/${pkgname}/archive/v${_actual_pkgver}.tar.gz")
md5sums=('05d77cca7a7a6240eaf4c09492baa31a')

check() {
    cd "${srcdir}/${pkgname}-${_actual_pkgver}"
    # fix a wrong path in the Makefile
    sed -i 's+holo-test+/usr/lib/holo/holo-test+' Makefile
    make check
}

package() {
    cd "${srcdir}/${pkgname}-${_actual_pkgver}"
    make install DESTDIR="${pkgdir}"
}
