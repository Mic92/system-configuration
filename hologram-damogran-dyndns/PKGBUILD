pkgname='hologram-damogran-dyndns'
pkgver=1.0.0
pkgrel=1
pkgdesc='hologram: DynDNS setup for Damogran (using Gandi API)'
arch=('any')
url=''
# custom:none refers to the gandi-dyndns script,
# cf. https://github.com/lembregtse/gandi-dyndns/issues/9
license=('AGPL3' 'custom:none')
depends=(
    'python2'
    'systemd'
)
builddepends=('perl')
source=(
    '.env'
    'gandi-dyndns.service'
    'gandi-dyndns.timer'
    'gandi_dyndns.py'
    'gandi_dyndns.sh'
    'gandi_dyndns_config.json.in'
    'gandi_dyndns_providers.json'
)

build() {
    # check that all configuration is present
    source "${srcdir}/.env"
    if [ -z "$GANDI_APIKEY" -o -z "$GANDI_DOMAIN" -o -z "$GANDI_SUBDOMAINS" ]; then
        echo "Some variables are not defined by .env; stopping." >&2
        return 1
    fi

    # replace configuration variables in source files
    perl -pe 's{\$\{(\w+?)\}}{$ENV{$1}//$&}eg' \
        < "${srcdir}/gandi_dyndns_config.json.in" \
        > "${srcdir}/gandi_dyndns_config.json"
}

package() {
    install -D -m 0644 gandi_dyndns.py             "${pkgdir}/usr/lib/gandi_dyndns.py"
    install -D -m 0755 gandi_dyndns.sh             "${pkgdir}/usr/bin/gandi_dyndns"
    install -D -m 0644 gandi_dyndns_config.json    "${pkgdir}/etc/gandi_dyndns/config.json"
    install -D -m 0644 gandi_dyndns_providers.json "${pkgdir}/etc/gandi_dyndns/providers.json"
    install -D -m 0644 gandi-dyndns.timer          "${pkgdir}/usr/lib/systemd/system/gandi-dyndns.timer"
    install -D -m 0644 gandi-dyndns.service        "${pkgdir}/usr/lib/systemd/system/gandi-dyndns.service"

    mkdir -p "${pkgdir}/etc/systemd/system/timers.target.wants/"
    ln -sf /usr/lib/systemd/system/gandi-dyndns.timer "${pkgdir}/etc/systemd/system/timers.target.wants/gandi-dyndns.timer"
}
