pkgname='hologram-catalyst'
pkgver=1.1.0
pkgrel=1
pkgdesc='hologram: enable Catalyst driver'
arch=('any')
url=''
license=('AGPL3')
depends=() # runtime dependencies are listed below
source=(
    '20-radeon.conf'
    'systemd-boot-entry.conf.holoscript'
)
install='hologram-catalyst.install'

build() {
    cd "${srcdir}"
    echo 'blacklist radeon'  > modprobe.conf
    echo 'blacklist amdgpu' >> modprobe.conf # don't enable it until Catalyst is ready for it
}

package() {
    depends=(
        'holo>=0.4'
        'hologram-catalyst-repo'
        'catalyst-hook'
        'catalyst-libgl'
        'catalyst-utils'
        'opencl-catalyst'
        'lib32-catalyst-libgl'
        'lib32-catalyst-utils'
        'lib32-opencl-catalyst'
    )

    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants/"
    ln -s "/usr/lib/systemd/system/catalyst-hook.service" "${pkgdir}/etc/systemd/system/multi-user.target.wants/catalyst-hook.service"

    install -D -m 0644 20-radeon.conf                     "${pkgdir}/etc/X11/xorg.conf.d/20-radeon.conf"
    install -D -m 0644 modprobe.conf                      "${pkgdir}/etc/modprobe.d/catalyst.conf"
    install -D -m 0755 systemd-boot-entry.conf.holoscript "${pkgdir}/usr/share/holo/repo/20-catalyst/boot/loader/entries/arch.conf.holoscript"
}
