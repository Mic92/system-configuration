pkgname='holo-build'
_actual_pkgver=1.0-beta.1
pkgver=${_actual_pkgver//-/}
pkgrel=1
pkgdesc='Cross-distribution system package compiler'
arch=('i686' 'x86_64')
url='http://holocm.org'
license=('GPL3')
depends=(
    'fakeroot'
    'pacman' # used by the pacman generator to resolve package groups
)
makedepends=('go' 'perl')
source=("https://github.com/holocm/${pkgname}/releases/download/v${_actual_pkgver}/${pkgname}-${_actual_pkgver}.tar.gz")
md5sums=('225263a02ef4dc29fa4faf7a7eb62e8b')

build() {
    cd "${srcdir}/${pkgname}-${_actual_pkgver}"
    make
}

check() {
    cd "${srcdir}/${pkgname}-${_actual_pkgver}"
    make check
}

package() {
    cd "${srcdir}/${pkgname}-${_actual_pkgver}"
    make install DESTDIR="${pkgdir}"
}
